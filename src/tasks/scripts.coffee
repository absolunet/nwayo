#debug  = require 'gulp-debug'
gulp   = require 'gulp'
rename = require 'gulp-rename'

util = require './_util'
path = util.path

cache_path       = "#{path.dir.cache}/#{path.build.scripts}"
konstan_filename = 'konstan'


#-- Lint JS
gulp.task 'scripts_lint', ->
	jshint  = require 'gulp-jshint'
	stylish = require 'jshint-stylish'

	return gulp.src path.files.scripts_lint
		.pipe jshint()
		.pipe jshint.reporter(stylish)
		.pipe jshint.reporter('fail')




#-- Convert constants to JS
gulp.task 'scripts_constants', ->
	merge = require 'merge-stream'

	streams = []
	for bname, bundle of util.bundles

		data = {
			nwayo:   util.pkg.nwayo.version
			project: util.pkg.name
			bundle:  bname
			konstan: util.parse_konstan 'scripts', bundle.output.url
		}

		streams.push(
			util.vinyl_stream "#{konstan_filename}.js", "var konstan = #{JSON.stringify data, null, '\t'};"
				.pipe gulp.dest "#{cache_path}/#{bname}"
		)

	return merge.apply null, streams



#-- Compile
gulp.task 'scripts_compile', ['scripts_lint', 'scripts_constants'], ->
	merge   = require 'merge-stream'
	include = require 'gulp-nwayo-include'
	replace = require 'gulp-replace'
	gulpif  = require 'gulp-if'
	uglify  = require 'gulp-uglify'

	streams = []
	for bname, bundle of util.bundles

		# for each collection
		for collection, list of bundle.scripts.collections

			# resolve konstan real filepath
			pos = list.indexOf 'konstan'
			list[pos] = "#{cache_path}/#{bname}/#{konstan_filename}" if pos isnt -1

			# require each file
			list[i] = "//= require #{item}" for item, i in list
			source = """
				/*!\n * Generated by nwayo #{util.pkg.nwayo.version} for #{util.pkg.name}:#{bname}\n */\n
				(function(global, undefined) {
					#{list.join '\n'}
				})(typeof window !== 'undefined' ? window : this);
			"""

			streams.push(
				util.vinyl_stream "#{collection}.js", source
					.pipe include basePath: './', autoExtension:true
					.pipe gulpif( bundle.scripts.options.minify, uglify(preserveComments:'some') )
					.pipe gulp.dest "#{bundle.output.build}/#{path.build.scripts}"
			)

	return merge.apply null, streams




#-- Rebuild
gulp.task 'scripts', (cb) ->
	del         = require 'del'
	runsequence = require 'run-sequence'

	list = []
	list.push "#{bundle.output.build}/#{path.build.scripts}", "#{cache_path}/#{bname}" for bname, bundle of util.bundles

	del.sync list, force:true
	runsequence 'scripts_compile', cb
