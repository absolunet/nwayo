#debug  = require 'gulp-debug'
gulp = require 'gulp'

PATH = global.nwayo.path
ENV  = global.nwayo.env
Util = global.nwayo.util




#-- Convert constants to JSON
gulp.task 'local_constants', ->
	merge = require 'merge-stream'

	streams = []
	for bname, bundle of ENV.bundles

		data = {
			GENERATION: "Generated by nwayo #{ENV.pkg.nwayo.version} for #{ENV.pkg.name}:#{bname}"
			nwayo:      ENV.pkg.nwayo.version
			project:    ENV.pkg.name
			bundle:     bname
			konstan:    Util.parseKonstan 'local', bundle.output.url
		}

		streams.push(
			Util.vinylStream PATH.filename.konstanLocal, JSON.stringify(data, null, 2)
				.pipe gulp.dest bundle.output.konstan
		)

	return merge.apply null, streams



#-- Rebuild
gulp.task 'local', (cb) ->
	del         = require 'del'
	runsequence = require 'run-sequence'

	list = []
	list.push "#{bundle.output.konstan}/#{PATH.filename.konstanLocal}" for bname, bundle of ENV.bundles

	del list, force:true, ->
		runsequence ['local_constants'], cb
